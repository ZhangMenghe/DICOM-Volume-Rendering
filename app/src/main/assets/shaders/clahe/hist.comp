#version 310 es

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;	// 64 threads

// input Dicom Volume
layout(binding = 0, r32ui) readonly uniform mediump uimage3D volume;
// input LUT
layout(std430, binding = 1) buffer lutBuffer {
    uint LUT[];
};

// output Histogram
layout(std430, binding = 2) buffer outHist {
    int hist[];
};
// output histogram max values 
layout(std430, binding = 3) buffer outHistMax {
	int histMax[];
};

uniform ivec3 numSB;		// number of Sub Blocks
uniform uint NUM_OUT_BINS;	// number of gray values in the new Volume
uniform uvec3 offset;		// start of the region to apply CLAHE to
uniform bool useLUT;		// if we need to use the LUT to map to a different bit range
uniform uvec3 volumeDims;	// size of the section of the volume we are applying CLAHE to 

void main() {

	// figure out the sub block this index belongs to 
	uvec3 index = gl_GlobalInvocationID.xyz;
	ivec3 sizeSB = ivec3( ivec3(volumeDims) / numSB );	
	ivec3 currSB = ivec3( ivec3(index) / sizeSB );

	// if we are not within the volume of interest -> return 
	if ( index.x >= volumeDims.x || index.y >= volumeDims.y || index.z >= volumeDims.z ) {
		return;
	}

	// get the gray value of the Volume
	uint volSample = imageLoad( volume, ivec3(index + offset) ).x;
	uint histIndex = uint(currSB.z * numSB.x * numSB.y + currSB.y * numSB.x + currSB.x);
	
	// Increment the appropriate histogram
	uint grayIndex = (NUM_OUT_BINS * histIndex) + volSample;
	if (useLUT){
		grayIndex = (NUM_OUT_BINS * histIndex) + LUT[ volSample ];
	}
	atomicAdd( hist[ grayIndex ], 1 );

	// update the histograms max value
	atomicMax( histMax[ histIndex ], hist[ grayIndex ] );

}